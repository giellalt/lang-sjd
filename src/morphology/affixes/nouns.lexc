! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2018 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Kildin Saami noun inflection


LEXICON KOOREB
+N+Sg+Nom:%>0 K ;
+N+Sg+Ill:%^EPHV%>а K ;
+N+Sg+Loc:%^EPHV%>эсьт K ;

LEXICON KNIIGA
+N+Sg+Nom: K ;
+N+Sg+Gen: K ;
+N+Sg+Acc: K ;
+N+Pl+Nom: K ;
+N+Pl+Gen: K ;
+N+Pl+Acc:%>тҍ K ;

LEXICON SYJJT
+N+Sg+Nom: K ;
+N+SG+Gen:%^WG K ;
+N+Sg+Acc:%^WG K ;
+N+Sg+Ill:%>а K ;
+N+Pl+Nom:%^WG K ;
+N+Pl+Loc:%^WG%>энҍ K ;


!! "class1" = monosyllabic/non-palatalized inflection foot, str-wk, no ablaut/umlaut, palatalization in Ill.Sg
LEXICON 1DA !!= @CODE@ is orthographic subclass with xx--> x

  +N:     SgNomSuf    ;
  +N:%^WG SgGenAccSuf ;
! +N:%^DI SgIllSufJE  ; !MR:here is no gradation, but what was %^DI? I'll check this later
  +N:     SgIllSufJE  ;
  +N:%^WG SgLocSuf    ;
  +N:%^WG SgAbeSuf    ;
  +N:%^WG SgComSuf    ;
  +N:     EssSuf      ;
  +N:     ParSufE      ;

  +N:%^WG PlNomSuf    ;
  +N:%^WG PlGenSuf    ;
  +N:%^WG PlAccSuf    ;
  +N:%^WG PlIllSuf    ;
  +N:%^WG PlLocSuf    ;
  +N:%^WG PlAbeSuf    ;
  +N:%^WG PlComSuf    ;

LEXICON 1DB !!= @CODE@ is orthographic subclass with xy-->x and "half-palatalization" in Ill.Sg;
!! OBS! SgIllSufJE vs. SgIllSufHalfE does not mark a true morphological subclass, but is due to orthographic conventions

 +N:     SgNomSuf      ;
 +N:%^WG SgGenAccSuf   ;
!+N:%^DI   SgIllSufHalfE ; %^DI? is a "placeholder"? I have to check why we needed this in the earlier version
 +N:     SgIllSufHalfE ;
 +N:%^WG SgLocSuf      ;
 +N:%^WG SgAbeSuf      ;
 +N:%^WG SgComSuf      ;
 +N:     EssSuf        ;
 +N:     ParSufE        ;

 +N:%^WG PlNomSuf      ;
 +N:%^WG PlGenSuf      ;
 +N:%^WG PlAccSuf      ;
 +N:%^WG PlIllSuf      ;
 +N:%^WG PlLocSuf      ;
 +N:%^WG PlAbeSuf      ;
 +N:%^WG PlComSuf      ;

!! Lexica 2D etc are classes from Kuruch dictionary.
!! It is unclear whether this is the correct classification

LEXICON 2D !!= @CODE@ like 1D ?  these are just dummy for the moment
 +N:     SgNomSuf    ; ! Им.
 +N:%^WG SgGenAccSuf ; ! Род., Вин.
 +N:%^DI SgIllSufJE  ; ! Дат.-напр
 +N:%^WG SgLocSuf    ; ! Местн.
 +N:%^WG SgAbeSuf    ; ! Лишит.
 +N:%^WG SgComSuf    ; ! Совм.
 +N:     EssSuf      ; ! Превр.
 +N:     ParSufE      ; ! Парт.

 +N:%^WG PlNomSuf    ; ! Им.
 +N:%^WG PlGenSuf    ; ! Вин.
 +N:%^WG PlAccSuf    ; ! Вин.
 +N:%^WG PlIllSuf    ; ! Местн.
 +N:%^WG PlLocSuf    ; ! Местн.
 +N:%^WG PlAbeSuf    ; ! Лишит.
 +N:%^WG PlComSuf    ; ! Совм.

LEXICON 3D !!= @CODE@
 +N:     SgNomSuf    ; ! Им.
 +N:%^WG SgGenAccSuf ; ! Род., Вин.
 +N:%^DI SgIllSufJE  ; ! Дат.-напр
 +N:%^WG SgLocSuf    ; ! Местн.
 +N:%^WG SgAbeSuf    ; ! Лишит.
 +N:%^WG SgComSuf    ; ! Совм.
 +N:     EssSuf      ; ! Превр.
 +N:     ParSufE      ; ! Парт.

 +N:%^WG PlNomSuf    ; ! Им.
 +N:%^WG PlGenSuf    ; ! Вин.
 +N:%^WG PlAccSuf    ; ! Вин.
 +N:%^WG PlIllSuf    ; ! Местн.
 +N:%^WG PlLocSuf    ; ! Местн.
 +N:%^WG PlAbeSuf    ; ! Лишит.
 +N:%^WG PlComSuf    ; ! Совм.

LEXICON 4D  !!= @CODE@ is contract
     +N:%^WG     SgNomSuf    ; ! Им.
     +N:%^MON	 SgGenAccPlNomGenSuf ; ! Род.
     +N:%^MON	 EssSuf      ; ! Превр.
     +N:%^MON	 SgLocSuf    ; ! Местн.
     +N:%^MON%>ъ SgIllSufJE  ; !hard sign insertion needs a twolc-rule
     +N:%^MON     SgAbeSufAHTA    ; ! Лишит.
     +N:%^MON     SgComSuf    ; ! Совм.
	 +N:%^MON%>ъ ParSufJEDDJE ; !hard sign insertion needs a twolc-rule
     +N:%^MON     PlAccSuf    ; ! Вин.
     +N:%^MON     PlLocSuf    ; ! Местн.
     +N:%^MON     PlAbeSuf    ; ! Лишит.
     +N:%^MON     PlComSuf    ; ! Совм.

LEXICON 4DА  !!= @CODE@
 +N+Sg+Nom:%^WG         K ; ! Им.
 +N+Sg+Gen:%^DI%>э      K ; ! Род.
 +N+Sg+Acc:%^WG         K ; ! Вин.
    +N+Ess:%>энҍ        K ; ! Превр.
 +N+Sg+Loc:%^WG%>эсьт   K ; ! Местн.
 +N+Sg+Ill:%^DI%>а      K ; ! Дат.-напр
 +N+Sg+Ela:%^WG%>ха     K ; ! Лишит.
 +N+Sg+Com:%^WG%>энҍ    K ; ! Совм.
    +N+Par:%>ко         K ; ! Not in Kuruch

 +N+Pl+Nom:%^WG         K ; ! Им.
 +N+Pl+Acc:%^WG%>этҍ    K ; ! Вин.
 +N+Pl+Loc:%^WG%>энҍ    K ; ! Местн.
 +N+Pl+Ela:%^WG%>эха    K ; ! Лишит.
 +N+Pl+Com:%^WG%>эгуэйм K ; ! Совм.

LEXICON 5D  !!= @CODE@ goes to 1DA
  1DA ;

LEXICON OAHPA_N  !!= @CODE@ is all the words from OAHPA_N, we just point them to 1DA.
  1DA ; ! Just pointing it on to the first and best
  
LEXICON ELAN_N  !!= @CODE@ is all the words from ELAN_N, we just point them to 1DA.
  1DA ; ! Just pointing it on to the first and best


!! !!Case affixes

LEXICON SgNomSuf !!= @CODE@
 +Sg+Nom:%>0 K ; ! Им.
 
LEXICON SgGenAccPlNomGenSuf
+Pl+Nom:%>э K ;
 +Sg+Gen:%>э K ; ! Род.
 +Sg+Acc:%>э K ; ! Вин.
 +Pl+Gen:%>э K ; 

LEXICON SgGenAccSuf !!= @CODE@
 +Sg+Gen:%>0 K ; ! Род.
 +Sg+Acc:%>0 K ; ! Вин.

LEXICON EssSuf !!= @CODE@
 +Ess:%>энҍ K ;
!! Превр. !check here and elsewhere whether the palatal-mark-deletion-rule is really needed here (it's already included above)

LEXICON ParSufE  !!= @CODE@
 +Par:%>э K ; ! Парт.

LEXICON ParSufHalfE  !!= @CODE@
 +Par:%>ӭ K ; !they are allomorphs only in orthography
 
LEXICON ParSufJEDDJE
+Par:%>едтӭ K ;

LEXICON SgLocSuf  !!= @CODE@
 +Sg+Loc:%>эсьт K ; ! Местн.

LEXICON SgIllSufA  !!= @CODE@
 +Sg+Ill:%>а K ;

LEXICON SgIllSufE !!= @CODE@
 +Sg+Ill:%>э K ;

LEXICON SgIllSufJE !!= @CODE@
 +Sg+Ill:%>е K ;

LEXICON SgIllSufHalfE !!= @CODE@
 +Sg+Ill:%>ӭ K ; ! Дат.-напр

LEXICON SgAbeSuf !!= @CODE@
 +Sg+Abe:%>ха K ; ! Лишит.
 
LEXICON SgAbeSufAHTA
+Sg+Abe:%>аһта K ;

LEXICON SgComSuf !!= @CODE@
 +Sg+Com:%>энҍ K ; ! Совм.

LEXICON PlNomSuf !!= @CODE@
 +Pl+Nom: K ; ! Им.

LEXICON PlGenSuf !!= @CODE@
 +Pl+Gen:%>э   K ;
 +Pl+Gen:%>этҍ K ; !innovative variant identical to PlAccSuf

LEXICON PlAccSuf !!= @CODE@
 +Pl+Acc:%>этҍ K ; ! Вин.

LEXICON PlIllSuf !!= @CODE@
 +Pl+Ill:%>этҍ K ; ! Местн.

LEXICON PlLocSuf !!= @CODE@
 +Pl+Loc:%>энҍ K ; ! Местн.

LEXICON PlAbeSuf !!= @CODE@
 +Pl+Abe:%>эха K ; ! Лишит.

LEXICON PlComSuf !!= @CODE@
 +Pl+Com:%>эгуэйм K ; ! Совм.
