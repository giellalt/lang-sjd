! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2018 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no


LEXICON Pronoun
Demonstratives ;
Determiners ;
Personals ;
Interrogatives ;
Relatives ;
Reflexives ;


LEXICON Demonstratives
+Pron+Dem+Disc:та dem ;
+Pron+Dem+Prox:тэ dem ;
+Pron+Dem+Dist:ту dem ;

LEXICON dem
+Sg+Nom:дт K ;
+Sg+Nom:дта K ; !yet unclear what their distribution and function is
+Sg+Gen:нн K ;
+Sg+Gen:нна K ; !yet unclear what their distribution and function is
+Sg+Acc:нн K ;
+Sg+Acc:нна K ; !yet unclear what their distribution and function is
+Sg+Ill:з K ;
+Sg+Loc:сьт K ;
+Sg+Com:йн K ;
+Sg+Abe:ннха K ;
+Sg+Ess:йн K ;
+Pl+Nom:гк K ;
+Long+Pl+Nom:гка K ; !yet unclear what their distribution and function is
+Pl+Gen:йт K ;
+Pl+Acc:йт K ;
+Pl+Ill:йт K ;
+Pl+Loc:йнэнҍ K ;
+Pl+Com:йтгуэйм K ;
+Pl+Abe:йтха K ;
+Pl+Ess:йнэнҍ K ;


LEXICON Determiners
+Pron+Det:манҍтэ K ;


! Personal pronouns
! =================
! a = stem му-, то̄-, со̄-
! b = stem      тo-, сo-
! c = stem мы-, ты-, сы-
! d = stem мӣ-, тӣ-, сӣ-

LEXICON Personals
+Pron+Pers:му pers_a ;
+Pron+Pers:то̄ pers_a ;
+Pron+Pers:со̄ pers_a ;
+Pron+Pers:то pers_b ;
+Pron+Pers:со pers_b ;
+Pron+Pers:мы pers_c ;
+Pron+Pers:ты pers_c ;
+Pron+Pers:сы pers_c ;
+Pron+Pers:мӣ pers_d ;
+Pron+Pers:тӣ pers_d ;
+Pron+Pers:сӣ pers_d ;

LEXICON pers_a
+Sg+Nom:нн K ;
+Sg+Gen:н K ;
+Sg+Acc:н K ;
+Sg+Ill:ннӭ K ;
+Sg+Loc:нэсьт K ;
+Sg+Loc:ст K ;
+Sg+Com:нэнҍ K ;
+Sg+Abe:нха K ;
+Sg+Ess:ннэнҍ K ;

LEXICON pers_b
+Sg+Ill:ннӭ K ;
+Sg+Loc:ст K ;

LEXICON pers_c
+Sg+Ill:ннӭ K ;
+Pl+Nom:йй K ;
+Pl+Ill:йе K ;

LEXICON pers_d
+Pl+Gen:н K ;
+Pl+Acc:нэтҍ K ;
+Pl+Loc:нэнҍ K ;
+Pl+Com:нэгуэйм K ;
+Pl+Abe:нэха K ; 
+Pl+Ess:ннэнҍ K ;


! Interrogative pronouns
! ======================
! a = stem ке̄-, мӣ-
! b = stem мэ̄-
! c = stem ма̄-
! d = stem коамп-

LEXICON Interrogatives
+Pron+Interr:ке̄ interr_a ;
+Pron+Interr:мӣ interr_a ;
+Pron+Interr:мэ̄ interr_b ;
+Pron+Interr:ма̄ interr_c ;
+Pron+Interr:коампэ interr_d ;


LEXICON interr_a
+Sg+Nom: K ;
+Sg+Gen:нн K ;
+Sg+Acc:нн K ;
+Sg+Ill:з K ;
+Sg+Loc:сьт K ;
+Sg+Com:йн K ;
+Sg+Abe:ннха K ;
+Sg+Ess:йн K ;
+Pl+Nom:гк K ;
+Pl+Gen:йт K ;
+Pl+Acc:йт K ;
+Pl+Ill:йт K ;
+Pl+Loc:йн K ;
+Pl+Com:йтгуэйм K ;
+Pl+Abe:йтха K ;
+Pl+Ess:йнэнҍ K ;

LEXICON interr_b
+Sg+Gen:нн K ;
+Sg+Acc:нн K ;
+Sg+Ill:з K ;
+Sg+Com:йн K ;
+Sg+Abe:ннха K ;
+Sg+Ess:йн K ;
+Pl+Nom:гк K ;
+Pl+Gen:йт K ;
+Pl+Acc:йт K ;
+Pl+Ill:йт K ;
+Pl+Loc:йн K ;
+Pl+Com:йтгуэйм K ;
+Pl+Abe:йтха K ;
+Pl+Ess:йнэнҍ K ;

LEXICON interr_c
+Sg+Loc:сьт K ;

LEXICON interr_d
+Du+Nom: K ;
+Du+Loc:сьт K ;
+Du+Com:нҍ K ;
+Du+Abe:ха K ;
! what about other case forms?
! what about коампнессе, коампнезэсьт, коампнезэ?


! Relative pronoun
! =================
! a = stem кӯ-
! b = stem ко̄-

LEXICON Relatives
кӯ:кӯ rel_a ;
кӯ:ко̄ rel_b ;

LEXICON rel_a
+Pron+Rel+Sg+Nom: K ;
+Pron+Rel+Pl+Nom:гк K ;

LEXICON rel_b
+Pron+Rel+Sg+Gen:нн K ;
+Pron+Rel+Sg+Acc:нн K ;
+Pron+Rel+Sg+Ill:з K ;
+Pron+Rel+Sg+Ill:нн K ;
+Pron+Rel+Sg+Loc:ннэсьт K ;
+Pron+Rel+Sg+Com:йн K ;
+Pron+Rel+Sg+Ess:йн K ;
+Pron+Rel+Sg+Abe:ннха K ;
+Pron+Rel+Pl+Gen:йт K ;
+Pron+Rel+Pl+Acc:йт K ;
+Pron+Rel+Pl+Ill:йт K ;
+Pron+Rel+Pl+Loc:йн K ;
+Pron+Rel+Pl+Com:йтгуэйм K ;
+Pron+Rel+Pl+Abe:йтха K ;
+Pron+Rel+Pl+Ess:йнэнҍ K ;


LEXICON Reflexives
ӣджь+Pron+Refl:ӣджь STRONGREFL ;
ӣджь+Pron+Refl:ӣжь WEAKREFL ;

LEXICON STRONGREFL
+Pron+Refl+Sg+Nom: K ;
+Pron+Refl+Ess:%>на PERSREFLSG ;
+Pron+Refl+Ess:%>ена PERSREFLPL ;  
+Pron+Refl+Ill:%>с PERSREFLSG ;
 
LEXICON WEAKREFL 
+Pron+Refl+Pl+Nom:%>а K ;
+Pron+Refl+Gen: PERSREFLSG ;
+Pron+Refl+Acc: PERSREFLSG ;
+Pron+Refl+Loc:%>с PERSREFLSG ;
+Pron+Refl+Com:%>ен PERSREFLSG ;
+Pron+Refl+Abe: PERSREFLSGABESS ;

+Pron+Refl+Gen:%>ед PERSREFLPL ;
+Pron+Refl+Acc:%>ед PERSREFLPL ;
+Pron+Refl+Loc:%>ес PERSREFLPL ;
+Pron+Refl+Com:%>ед PERSREFLPLCOM ;
+Pron+Refl+Abe:%>ед PERSREFLPLABESS ;

LEXICON PERSREFLSG
+Pron+Refl+Sg1:%>aн # ;
+Pron+Refl+Sg2:%>aнт # ;
+Pron+Refl+Sg3:%>есь # ;

LEXICON PERSREFLPL
+Pron+Refl+Pl1:%>ан # ;
+Pron+Refl+Pl2:%>ант # ;
+Pron+Refl+Pl3:%>есь # ;

LEXICON PERSREFLSGABESS
+Pron+Refl+Sg1:%>aнха # ;
+Pron+Refl+Sg2:%>aнтха # ;
+Pron+Refl+Sg3:%>есьха # ;

LEXICON PERSREFLPLABESS
+Pron+Refl+Pl1:%>анха # ;
+Pron+Refl+Pl2:%>антха # ;
+Pron+Refl+Pl3:%>есьха # ;

LEXICON PERSREFLPLCOM
+Pron+Refl+Pl1:%>ангуэйм # ;
+Pron+Refl+Pl2:%>антгуэйм # ;
+Pron+Refl+Pl3:%>есьгуэйм # ;


!Since pronouns can be followed by clitics (like =ше and the like) the rules above do not end with #; for reflexive pronouns, this has to be checked
