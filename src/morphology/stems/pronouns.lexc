! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2018 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no


LEXICON Pronoun
Demonstrative ;
Determiner ;
Personal ;
Interrogative ;
Relative ;
Reflexive ;


LEXICON Demonstrative
+Pron+Dem+Disc:та dem ;
+Pron+Dem+Prox:тэ dem ;
+Pron+Dem+Dist:ту dem ;

LEXICON dem
+Sg+Nom:дт K ;
+Sg+Nom:дта K ; !yet unclear what their distribution and function is
+Sg+Gen:нн K ;
+Sg+Gen:нна K ; !yet unclear what their distribution and function is
+Sg+Acc:нн K ;
+Sg+Acc:нна K ; !yet unclear what their distribution and function is
+Sg+Ill:з K ;
+Sg+Loc:сьт K ;
+Sg+Com:йн K ;
+Sg+Abe:ннха K ;
+Sg+Ess:йн K ;
+Pl+Nom:гк K ;
+Long+Pl+Nom:гка K ; !yet unclear what their distribution and function is
+Pl+Gen:йт K ;
+Pl+Acc:йт K ;
+Pl+Ill:йт K ;
+Pl+Loc:йнэнҍ K ;
+Pl+Com:йтгуэйм K ;
+Pl+Abe:йтха K ;
+Pl+Ess:йнэнҍ K ;


LEXICON Determiner
+Pron+Det:манҍтэ K "какой / which (kind)" ;
+Pron+Det+Use/Dial:манҍтэм K "какой / which (kind)" ;
+Pron+Det:мудта K "иной / different" ;
+Pron+Det:мугка K "иной / different" ; !plural
+Pron+Det:пугк K "всё / all" ;


! Personal pronouns
! =================
! a = stem му-, то̄-, со̄-
! b = stem      тo-, сo-
! c = stem мы-, ты-, сы-
! d = stem мӣ-, тӣ-, сӣ-

LEXICON Personal
+Pron+Pers:му pers_a ;
+Pron+Pers:то̄ pers_a ;
+Pron+Pers:со̄ pers_a ;
+Pron+Pers:то pers_b ;
+Pron+Pers:со pers_b ;
+Pron+Pers:мы pers_c ;
+Pron+Pers:ты pers_c ;
+Pron+Pers:сы pers_c ;
+Pron+Pers:мӣ pers_d ;
+Pron+Pers:тӣ pers_d ;
+Pron+Pers:сӣ pers_d ;

LEXICON pers_a
+Sg+Nom:нн K ;
+Sg+Gen:н K ;
+Sg+Acc:н K ;
+Sg+Ill:ннӭ K ;
+Sg+Loc:нэсьт K ;
+Sg+Loc:ст K ;
+Sg+Com:нэнҍ K ;
+Sg+Abe:нха K ;
+Sg+Ess:ннэнҍ K ;

LEXICON pers_b
+Sg+Ill:ннӭ K ;
+Sg+Loc:ст K ;

LEXICON pers_c
+Sg+Ill+Use/Dial:ннӭ K ;
+Pl+Nom:йй K ;
+Pl+Ill:йе K ;

LEXICON pers_d
+Pl+Gen:н K ;
+Pl+Acc:нэтҍ K ;
+Pl+Loc:нэнҍ K ;
+Pl+Com:нэгуэйм K ;
+Pl+Abe:нэха K ; 
+Pl+Ess:ннэнҍ K ;


! Interrogative pronouns
! ======================
! a = stem ке̄-, мӣ-
! b = stem мэ̄-
! c = stem ма̄-
! d = stem коамп-

LEXICON Interrogative
+Pron+Interr:ке̄ interr_a ;
+Pron+Interr:мӣ interr_a ;
+Pron+Interr:мэ̄ interr_b ;
+Pron+Interr:ма̄ interr_c ;
+Pron+Interr:коампэ interr_d ;

LEXICON interr_a
+Sg+Nom: K ;
+Sg+Gen:нн K ;
+Sg+Acc:нн K ;
+Sg+Ill:з K ;
+Sg+Loc:сьт K ;
+Sg+Com:йн K ;
+Sg+Abe:ннха K ;
+Sg+Ess:йн K ;
+Pl+Nom:гк K ;
+Pl+Gen:йт K ;
+Pl+Acc:йт K ;
+Pl+Ill:йт K ;
+Pl+Loc:йн K ;
+Pl+Com:йтгуэйм K ;
+Pl+Abe:йтха K ;
+Pl+Ess:йнэнҍ K ;

LEXICON interr_b
+Sg+Gen:нн K ;
+Sg+Acc:нн K ;
+Sg+Ill:з K ;
+Sg+Com:йн K ;
+Sg+Abe:ннха K ;
+Sg+Ess:йн K ;
+Pl+Nom:гк K ;
+Pl+Gen:йт K ;
+Pl+Acc:йт K ;
+Pl+Ill:йт K ;
+Pl+Loc:йн K ;
+Pl+Com:йтгуэйм K ;
+Pl+Abe:йтха K ;
+Pl+Ess:йнэнҍ K ;

LEXICON interr_c
+Sg+Loc:сьт K ;

LEXICON interr_d
+Du+Nom: K ;
+Du+Loc:сьт K ;
+Du+Com:нҍ K ;
+Du+Abe:ха K ;
! what about other case forms?
! what about коампнессе, коампнезэсьт, коампнезэ?


! Relative pronoun
! =================
! a = stem кӯ-
! b = stem ко̄-

LEXICON Relative
+Pron+Rel:кӯ rel_a ;
+Pron+Rel:ко̄ rel_b ;

LEXICON rel_a
+Sg+Nom: K ;
+Pl+Nom:гк K ;

LEXICON rel_b
+Sg+Gen:нн K ;
+Sg+Acc:нн K ;
+Sg+Ill:з K ;
+Sg+Ill:нн K ;
+Sg+Loc:ннэсьт K ;
+Sg+Com:йн K ;
+Sg+Ess:йн K ;
+Sg+Abe:ннха K ;
+Pl+Gen:йт K ;
+Pl+Acc:йт K ;
+Pl+Ill:йт K ;
+Pl+Loc:йн K ;
+Pl+Com:йтгуэйм K ;
+Pl+Abe:йтха K ;
+Pl+Ess:йнэнҍ K ;


LEXICON Reflexive
+Pron+Refl:ӣджь refl_pron ;

LEXICON refl_pron
+Nom+Sg1:%>0 K ;
+Nom+Sg2:%>0 K ;
+Nom+Sg3:%>0 K ;
+Nom+Pl1:%^WG%>а K ;
+Nom+Pl2:%^WG%>а K ;
+Nom+Pl3:%^WG%>а K ;
+Gen+Sg1:%^WG%>aн K ;
+Gen+Sg2:%^WG%>aнт K ;
+Gen+Sg3:%^WG%>есь K ;
+Gen+Pl1:%^WG%>едaн K ;
+Gen+Pl2:%^WG%>едaнт K ;
+Gen+Pl3:%^WG%>едӭсь K ;
+Acc+Sg1:%^WG%>aн K ;
+Acc+Sg2:%^WG%>aнт K ;
+Acc+Sg3:%^WG%>есь K ;
+Acc+Pl1:%^WG%>едан K ;
+Acc+Pl2:%^WG%>едант K ;
+Acc+Pl3:%^WG%>едӭсь K ;
+Loc+Sg1:%^WG%>сaн K ;
+Loc+Sg2:%^WG%>сaнт K ;
+Loc+Sg3:%^WG%>сесь K ;
+Loc+Pl1:%^WG%>eсaн K ;
+Loc+Pl2:%^WG%>eсaнт K ;
+Loc+Pl3:%^WG%>eсесь K ;
+Ill+Sg1:%>сан K ;
+Ill+Sg2:%>сант K ;
+Ill+Sg3:%>сесь K ;
+Ill+Pl1:%^WG%>едaн K ;
+Ill+Pl2:%^WG%>едaнт K ;
+Ill+Pl3:%^WG%>едӭсь K ;
+Com+Sg1:%^WG%>енан K ;
+Com+Sg2:%^WG%>енант K ;
+Com+Sg3:%^WG%>енесь K ;
+Com+Pl1:%^WG%>едангуэйм K ;
+Com+Pl2:%^WG%>едантгуэйм K ;
+Com+Pl3:%^WG%>едӭсьгуэйм K ;
+Abe+Sg1:%^WG%>aнха K ;
+Abe+Sg2:%^WG%>aнтха K ;
+Abe+Sg3:%^WG%>есьха K ;
+Abe+Pl1:%^WG%>едaнха K ;
+Abe+Pl2:%^WG%>едaнтха K ;
+Abe+Pl3:%^WG%>едӭсьха K ;
+Ess+Sg1:%>нан K ;
+Ess+Sg2:%>нант K ;
+Ess+Sg3:%>несь K ;
+Ess+Pl1:%>енан K ;
+Ess+Pl2:%>енант K ;
+Ess+Pl3:%>енесь K ;

!More effective rules (less redundency) are needed, e.g. many suffixes re-occur
