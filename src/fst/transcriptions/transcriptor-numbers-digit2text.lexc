!! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

! Converting sjd number words into arabic numerals
! and vice versa.

Multichar_Symbols

  +Use/NG 

LEXICON Root
NUMBERSECTION ; ! Numerals
COMMASECTION ;  ! Separator section 
! DATESECTION ;   ! Not implemented

LEXICON NUMBERSECTION  ! Numerals

HUNDREDSM ;		! 200M
1:чӯдтҍ%  HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
1:чӯдтҍ%  HUNDREDT ;	! 100000-100999 чӯдтҍ
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
1:тоафант%  THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
CARDINAL ;		  ! 1-9
ORDINAL ; ! tja, sjekk
AVTMUSS ;         ! 1. the isolated first.

LEXICON HUNDREDSM
2:кӯххт%   CUODIM ;
2:кӯһт% +Use/NG  CUODIM ;
3:ко̄ллм%  CUODIM ;
4:не̄лльй%  CUODIM ;
5:выдт%  CUODIM ;
6:кудт%  CUODIM ;
7:кыджемь%  CUODIM ;
8:ка̄ххц%  CUODIM ;
9:аххц%  CUODIM ;

LEXICON CUODIM
:чӯдтҍ%  HUNDREDM ;

LEXICON HUNDREDM
TENSM ;
TEENSM ;
%0: ONESM ;
%0%0: MILJON ;

LEXICON TEENSM
1: TEENM ;

LEXICON TEENM
1:э̄ххт%   LOHKAIM ;
2:кӯххт% +Use/NG  LOHKAIM ;
2:кӯһт%   LOHKAIM ;
3:ко̄ллм%   LOHKAIM ;
4:не̄лльй%   LOHKAIM ;
5:выдт%   LOHKAIM ;
6:кудт%   LOHKAIM ;
7:кыджемь%   LOHKAIM ;
8:ка̄ххц%   LOHKAIM ;
9:аххц%  LOHKAIM ;

LEXICON LOHKAIM
:лоагкь%  MILJON ;

LEXICON TENSM
1%0:лоагкь%  MILJON ;
2:кӯххт%  LOGIM ;
2:кӯһт% +Use/NG LOGIM ;
3:ко̄ллм%  LOGIM ;
4:не̄лльй%  LOGIM ;
5:выдт%  LOGIM ;
6:кудт%  LOGIM ;
7:кыджемь%  LOGIM ;
8:ка̄ххц%  LOGIM ;
9:аххц%  LOGIM ;

LEXICON LOGIM
%0:лоагкь%  MILJON ;
:лоагкь%  ONESM ;

LEXICON ONESM
1:миллион%  OVERTHOUSANDS ;
2:кӯххт%  MILJON ;
2:кӯһт% +Use/NG MILJON ;
3:ко̄ллм%  MILJON ;
4:не̄лльй%  MILJON ;
5:выдт%  MILJON ;
6:кудт%  MILJON ;
7:кыджемь%  MILJON ;
8:ка̄ххц%  MILJON ;
9:аххц%  MILJON ; 

LEXICON MILJON
:миллион%  OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
1:чӯдтҍ%  HUNDREDT ;	! 100.000.000
%0: TENST ;		!  20.000.000
%0: TEENST ;		!  10.000.000
%0%0: ONEST ;		!   2.000.000
%0%01:тоафант%  THOUSAND ;	!   1.000.000
%0%0%0: UNDERTHOUSAND ;	
%0%0%0%0: TENS ;
%0%0%0%0: TEENSCONT ;
%0%0%0%0%0: ONES ;
%0%0%0%0%0%0: COMMA ;	!  x000000

LEXICON HUNDREDST
2:кӯххт%   CUODIT ;	! 200000-299999
2:кӯһт% +Use/NG  CUODIT ;	! 200000-299999
3:ко̄ллм%  CUODIT ;	! 300000-399999
4:не̄лльй%  CUODIT ;	! 400000-499999
5:выдт%  CUODIT ;	! 500000-599999
6:кудт%  CUODIT ;	! 600000-699999
7:кыджемь%  CUODIT ;	! 700000-799999
8:ка̄ххц%  CUODIT ;	! 800000-899999
9:аххц%  CUODIT ;	! 900000-999999

LEXICON CUODIT
:чӯдҍ%  HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
%01:э̄ххт%  THOUSANDS ;
%0:%  ONEST ;		! XX0YYY
%0%0:%  THOUSANDS ;	! X00YYY

LEXICON TEENST
1:%  TEENT ;

LEXICON TEENT
%0:лоагкь%  THOUSANDS ;
1:э̄ххт%  LOHKAITE ;
2:кӯххт%  LOHKAITE ;
2:кӯһт% +Use/NG LOHKAITE ;
3:ко̄ллм%  LOHKAITE ;
4:не̄лльемплоагкь%  THOUSANDS ;
5:выдт%  LOHKAITE ;
6:кудт%  LOHKAITE ;
7:кыджемь%  LOHKAITJE ;
8:ка̄ххц%  LOHKAITE ;
9:аххц%  LOHKAITE ;

LEXICON LOHKAITE
:эмплоагкь%  THOUSANDS ;

LEXICON LOHKAITJE
:емплоагкь%  THOUSANDS ;

LEXICON TENST
2:кӯххтлоагкь%  LOGIT ;
2:кӯһтлоагкь% +Use/NG LOGIT ;
3:ко̄ллмлоагкь%  LOGIT ;
4:не̄лльйлоагкь%  LOGIT ;
5:выдтлоагкь%  LOGIT ;
6:кудтлоагкь%  LOGIT ;
7:кыджемьлоагкь%  LOGIT ;
8:ка̄ххцлоагкь%  LOGIT ;
9:аххцлоагкь%  LOGIT ;

LEXICON LOGIT
%0:%   THOUSANDS ;
1:э̄ххт%  THOUSANDS ;
ONEST ;

LEXICON ONEST
2:кӯххт%  THOUSANDS ;
2:кӯһт% +Use/NG THOUSANDS ;
3:ко̄ллм%  THOUSANDS ;
4:не̄лльй%  THOUSANDS ;
5:выдт%  THOUSANDS ;
6:кудт%  THOUSANDS ;
7:кыджемь%  THOUSANDS ;
8:ка̄ххц%  THOUSANDS ;
9:аххц%  THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
:тоафант%  THOUSAND ;

LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
%0: TENS ;	   ! 1020-1099
%0: TEENSCONT ;	   ! 1010-1019
%0%0: ONES ;	   ! 1001-1009
%0%0%0: COMMA ;	   ! 1000
%0%0%0%.:  COMMA ;	   ! 1000.

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
1:чӯдтҍ%  HUNDRED ;
HUNDREDS ;

LEXICON HUNDREDS
2:кӯххт%   CUODI ;
2:кӯһт% +Use/NG  CUODI ;
3:ко̄ллм%  CUODI ;
4:не̄лльй%  CUODI ;
5:выдт%  CUODI ;
6:кудт%  CUODI ;
7:кыджемь%  CUODI ;
8:ка̄ххц%  CUODI ;
9:аххц%  CUODI ;

LEXICON CUODI
:чӯдҍ%  HUNDRED ;

LEXICON HUNDRED
TENS ;
TEENSCONT ;
%0:%  ONES ;
%0%0:%  COMMA ;
%0%0%.:%  COMMA ; ! card = ord for x00



LEXICON TENS

1%0:лоагкь COMMA ; ! cardinal
1%0%.:лоагант COMMA ; ! ordinal

2:кӯххт LOGI ;
2:кӯһт+Use/NG LOGI ;
3:ко̄ллм LOGI ;
4:не̄лльй LOGI ;
5:выдт LOGI ;
6:кудт LOGI ;
7:кыджемь LOGI ;
8:ка̄ххц LOGI ;
9:аххц LOGI ;

LEXICON LOGI
%0:лоагкь COMMA ; ! cardinal
%0%.:лоагант COMMA ; ! ordinal
:лоагкь%  ONES ;

LEXICON TEENSCONT
:%   TEENS ;

LEXICON TEENS
1: TEENSCARDINALS ;
1: TEENSORDINALS ;

LEXICON TEENSCARDINALS
1:э̄ххт LOHKAIE ;
2:кӯххт LOHKAIE ;
2:кӯһт+Use/NG LOHKAIE ;
3:ко̄ллм LOHKAIE ;
4:не̄лльемплоагкь COMMA ;
5:выдт LOHKAIE ;
6:кудт LOHKAIE ;
7:кыджемь LOHKAIJE ;
8:ка̄ххц LOHKAIE ;
9:аххц LOHKAIE ;

LEXICON LOHKAIE
:эмплоагкь COMMA ;

LEXICON LOHKAIJE
:емплоагкь COMMA ;



LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
1:э̄ххт  COMMA ;
2:кӯххт  COMMA ;
2:кӯһт+Use/NG  COMMA ;
3:ко̄ллм  COMMA ;
4:не̄лльй  COMMA ;
5:выдт  COMMA ;
6:кудт COMMA ;
7:кыджемь  COMMA ;
8:ка̄ххц  COMMA ;
9:аххц COMMA ;


LEXICON ORDINAL		   ! sme, fix
1%.:авьтма+Use/NG     COMMA ; ! sme, fix
1%.:авьтмусс     COMMA ; ! sme, fix
ONESORDINALS ;

LEXICON ONESORDINALS
2%.:ныммьп   # ;	   ! sme, fix
3%.:куалмант   # ;	   ! sme, fix
4%.:ня̄льянт # ;	       ! sme, fix
5%.:выдант   # ;	   ! sme, fix
6%.:кудант   # ;	   ! sme, fix
7%.:кыджянт    # ;	   ! sme, fix
8%.:ка̄хцант   # ;	   ! sme, fix
9%.:ахцант    # ;	   ! sme, fix

LEXICON TEENSORDINALS
1:э̄ххт   TEENSORDINALEND ;
2:кӯххт  TEENSORDINALEND ;
2:кӯһт+Use/NG TEENSORDINALEND ;
3:ко̄ллм   TEENSORDINALEND ;
4:не̄лльй  TEENSORDINALEND ;
5:выдт  TEENSORDINALEND ;
6:кудт  TEENSORDINALEND ;
7:кыджемь TEENSORDINALEND ;
8:ка̄ххц   TEENSORDINALEND ;
9:аххц TEENSORDINALEND ;

LEXICON TEENSORDINALEND
%.:эмплоагант COMMA ;

LEXICON AVTMUSS
1%.:авьтмусс     COMMA ;   ! sme, fix
1%.:авьтма+Use/NG   COMMA ;



! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!   Comma section    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


LEXICON COMMA
 #            ; 
! COMMASECTION ;

 LEXICON COMMASECTION 
 # ;
!  % запятая% :,      NUMBERSECTION ; ! perhaps rus (Kildin?) for "comma"
!  % čuokkis% :%.   NUMBERSECTION ; ! these will overlap with dates
!  % kolon% :%:     NUMBERSECTION ;
!  % sárggis% :%-   NUMBERSECTION ; 
! % lea% :%=        NUMBERSECTION ; 
! % gráda% :%°      NUMBERSECTION ; 
! % paragráfa% :§   NUMBERSECTION ; 
!  % násti% :%*     NUMBERSECTION ; 
! % ja% :&          NUMBERSECTION ; 


