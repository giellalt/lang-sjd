! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! Kildin Sámi TWOLC file
!
! $Id$
!
!

Alphabet
  а А ӓ Ӓ а̄ А̄ б Б в В г Г д Д 
  е Е ё Ё е̄ Е̄ ё̄ Ё̄ ж Ж з З һ Һ
  !һк ҺК гк ГК !test
  и И ӣ Ӣ й Й ј Ј к К л Л ӆ Ӆ 
  м М ӎ Ӎ н Н ӊ Ӊ ӈ Ӈ о О о̄ О̄ 
  п П р Р ҏ Ҏ с С т Т у У ӯ Ӯ 
  ф Ф х Х ц Ц ч Ч ш Ш щ Щ ъ Ъ 
  ы Ы ь Ь ҍ Ҍ э Э ӭ Ӭ э̄ Э̄ ю Ю 
  ю̄ Ю̄ я Я я̄ Я̄ 
! сс:с vs. сс:з (last one is already defined as a rule below)
  %´:0 
  %,:%´ 
  %>:0
  %^1VOW:0 
  %^2VOW:0 
  %^3VOW:0 
  %^4VOW:0 
  %^5VOW:0 
  %^VOWSH:0
  %^WG:0 
  %^EPHV:0 
  %^DI:0
  %^CLDPAL:0 ;

Sets
 C  = б Б в В г Г д Д ж Ж з З һ Һ й Й ј Ј к К л Л ӆ Ӆ м М ӎ Ӎ н Н ӊ Ӊ ӈ Ӈ п П р Р ҏ Ҏ с С т Т ф Ф х Х ц Ц ч Ч ш Ш щ Щ ;
 V  = а А ӓ Ӓ а̄ А̄ е Е е̄ Е̄ ё Ё ё̄ Ё̄ и И ӣ Ӣ о О о̄ О̄ у У ӯ Ӯ ы Ы э Э ӭ Ӭ э̄ Э̄ ю Ю ю̄ Ю̄ я Я я̄ Я̄ ;
 PALMARK = ь Ь ҍ Ҍ ;
 VPLOS = г Г ;
 VLPLOS = к К ;
 PALCON = й Й ј Ј ч Ч ; ! can only by followed by PALVOW !so-called "palatalized C"
 PALVOW = е Е е̄ Е̄ ё Ё ё̄ Ё̄ и И ӣ Ӣ ю Ю ю̄ Ю̄ я Я я̄ Я̄ ; !so-called "palatalized V"
 HPLVOW = ӓ Ӓ ӭ Ӭ ; !so-called "half-palatalized V"
 NPLVOW = а А а̄ А̄ о О о̄ О̄ у У ӯ Ӯ ы Ы э Э э̄ Э̄ ; !so-called "non-palatalized V"
 
 Dummy  = %^WG %^DI %> ;


!Definitions
!,

Rules

"Qualitative CG1a" !this rule doesn't work yet
Cx:0 <=> _ Cx: (ь:) %^WG:0 ;
      where Cx in ( с ) ;

"Qualitative CG1b xx:y" !сс : з
Cx:Cy <=> Cx: _ (ь:) %^WG:0 ;
      where Cx in ( с )
            Cy in ( з ) 
      matched ;

"Preaspiration" !һк : гк
һ:Cy <=> _ Cx: (PALMARK:) %^WG:0 ;
      where Cx in ( к )
            Cy in ( г ) 
      matched ;

"Palatalization: Clusters ллк : лльк, etc."
  PALMARK:0 <=> [ л ] _ Cx %^CLDPAL:0 ;
			where Cx in ( к ) ;

"Quantitative CG"
 Cy:0 <=> Cx _ (PALMARK:) %^WG:0 ;
      where Cx in ( в ж з й л м н ӈ р ф х ш )
            Cy in ( в ж з й л м н ӈ р ф х ш ) 
      matched ; 
      
"Quantitative CG 2-Cns Cluster" !e.g. ?ма̄дт : ма̄д, ?ма̄дтҍ : ма̄дҍ
 Cy:0 <=> Cx _  (PALMARK:) %^WG:0 ;
      where Cx in ( б г д )  
            Cy in ( п к т )
      matched ;

"Quantitative CG hard 3-Cns Cluster" !e.g. са̄ррн : са̄рн 
 Cx:0 <=> Cx _ Cy (PALMARK:) %^WG:0 ;
      where Cx in ( л й м р )  
            Cy in ( к в ч н )
      matched ;

"Epenthetic vowel deletion"
Vx:0  <=> C _ C+ %^EPHV:0 %>: V ;
      where Vx in ( а я ) ; 

"Palatalised Suffix Vowel"
Vx:Vy <=> ь: ( Dummy:0 ) %>:0 _ ;
      where Vx in ( а А э Э у У )
            Vy in ( я Я е Е ю Ю ) 
      matched ;

"Palatal Mark Deletion"
 ь:0 <=> C: _ (Dummy:) %>:0 [ :я | :Я | :е | :Е | :и | :И | :ё | :Ё | :ю | :Ю ] ;
 
"ue Diphtongisation"
э:а <=> у _ C:*  %^DI:0  ;
