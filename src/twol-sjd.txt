! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! Kildin Sámi TWOLC file
!
! $Id$
!
!

Alphabet
  а А б Б в В г Г д Д е Е ж Ж 
  з З и И й Й к К л Л м М н Н 
  о О п П р Р с С т Т у У ф Ф 
  х Х ц Ц ч Ч ш Ш щ Щ ъ Ъ ы Ы 
  ь Ь э Э ю Ю я Я ё Ё ј Ј ҋ Ҋ 
  ҍ Ҍ ҏ Ҏ қ Қ ң Ң ӆ Ӆ ӊ Ӊ ӎ Ӎ 
  ӓ Ӓ
  а̄ А̄ е̄ Е̄ ӣ Ӣ о̄ О̄ ӯ Ӯ ы̄ Ы̄ э̄ Э̄ ю̄ Ю̄ я̄ Я̄
  %´:0 %,:%´ %>:0
  %^1VOW:0 %^2VOW:0 %^3VOW:0 %^4VOW:0 %^5VOW:0 %^VOWSH:0
  %^WG:0 %^EPHV:0 %^DI:0 ;

Sets
 C  = б Б в В г Г д Д ж Ж з З й Й к К л Л м М н Н п П р Р 
          с С т Т ф Ф х Х ц Ц ч Ч ш Ш щ Щ ъ Ъ ь Ь э Э ј Ј ҋ Ҋ 
          ҍ Ҍ ҏ Ҏ қ Қ ң Ң ӆ Ӆ ӊ Ӊ ӎ Ӎ ;
 V  = а А е Е и И о О у У ы Ы ю Ю я Я ё Ё ӓ Ӓ 
      а̄ А̄ е̄ Е̄ ӣ Ӣ о̄ О̄ ӯ Ӯ ы̄ Ы̄ ю̄ Ю̄ э̄ Э̄ я̄ Я̄ ;
 PALVOW = е Е и И ю Ю я Я ё Ё е̄ Е̄ ӣ Ӣ ю̄ Ю̄ я̄ Я̄ ;
 NPLVOW = а А о О у У ы Ы э Э а̄ А̄ о̄ О̄ ӯ Ӯ ы̄ Ы̄ э̄ Э̄ ;
 Dummy  = %^WG %^DI %> ;


!Definitions
!;

Rules

"Quantitative CG"
 Cx:0 <=> Cx _ (ь:) (C) %^WG:0 ;
      where Cx in ( в л с з ) ; 

"Quantitative CG 2-Cns Cluster"
 Cy:0 <=> Cx _  (ь:) %^WG:0 ;
      where Cx in ( д р )  
            Cy in ( т р )
      matched ;


"Quantitative CG 3-Cns Cluster"
 Cx:0 <=> Cx _ Cy (ь:) %^WG:0 ;
      where Cx in ( р )  
            Cy in ( н )
      matched ;

"Epenthetic vowel deletion"
Vx:0  <=> C _ C+ %^EPHV:0 %>: V ;
      where Vx in ( а я ) ; 

"Palatalised Suffix Vowel"
Vx:Vy <=> ь: ( Dummy:0 ) %>:0 _ ;
      where Vx in ( а А э Э ы Ы о О у У )
            Vy in ( я Я е Е и И ё Ё ю Ю ) 
      matched ;

"Palatal Mark Deletion"
 ь:0 <=> C: _ (Dummy:) %>:0 [ :я | :Я | :е | :Е | :и | :И | :ё | :Ё | :ю | :Ю ] ;


"ue Diphtongisation"
э:а <=> у _ C:*  %^DI:0  ;
